// bin-ln-auto
// Automatically parse every file in the given folder
// For every binary create a symlink in /bin


// init and import
hostComputer = get_shell.host_computer
import_code("dev-lib/dev-lib.src")

cliDef = {
  "argsDef": [ {
    "name": "source",
    "description": "Bin source folder path",
    "default": home_dir + "/src/dev",
  } ],
  "optionsDef": [ {
    "name": "help",
    "alias": "h",
    "description": "Display help message",
  } ],
  "description": "Create a symlink in /bin for every binary in folder",
}
cliDef.usage = "bin-ln-auto [--options] " + devLib.Cli.formatArg(cliDef.argsDef[0])
cli = devLib.Cli.init(cliDef)
args = cli.args

// functions
parseAndLink = function(folder)
  if folder.is_folder == 1 then
    files = folder.get_files
    folders = folder.get_folders
    for file in files
      if file.is_binary == 1 then
        symResult = file.symlink("/bin", file.name)
        if typeof(symResult) == "string" then
          print(file.name + " : " + symResult)
        end if
      end if
    end for
    for fold in folders
      parseAndLink(fold)
    end for
  end if
end function


// main
path = home_dir + "/src/dev"
if args.source then path = args.source
folder = hostComputer.File(path)
if folder == null or not folder.is_folder then exit("source path is not a folder")
parseAndLink(folder)
